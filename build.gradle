plugins {
    id 'net.researchgate.release' version '2.3.5'
}


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'signing'

group = 'io.github.kayr'
archivesBaseName = 'fuzzy-csv'
description = 'A groovy/java tabular Data (from CSV,SQL,JSON) processing library ' +
        'that supports fuzzy column matching,tranformations/merging/querying etc'


// Using a setter method
project.buildDir = 'target'

description = """"""

sourceCompatibility = 1.6
targetCompatibility = 1.6


release {
    failOnUnversionedFiles = false
}

repositories {
    // First check local cache before accessing central repository
    // mavenRepo name:'Local', urls: "file://" + System.properties['user.home'] + "/.m2/repository"
    mavenCentral()
}
dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.8'
    compile group: 'io.github.kayr', name: 'phrase-matcher', version: '0.1.7'
    compile group: 'com.opencsv', name: 'opencsv', version: '3.8'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile 'com.jakewharton.fliptables:fliptables:1.0.2'
    compileOnly 'org.apache.poi:poi-ooxml:3.16', {
        exclude group: 'stax', module: 'stax-api'
    }
    compileOnly 'org.apache.poi:ooxml-schemas:1.3', {
        exclude group: 'stax', module: 'stax-api'
    }
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.200'
    testCompile group: 'junit', name: 'junit', version: '4.7'
    testCompile group: 'org.bitbucket.mstrobel', name: 'procyon-reflection', version: '0.5.32'
    testCompile 'log4j:log4j:1.2.17'
    testCompile 'org.slf4j:slf4j-log4j12:1.7.25'
    testCompile 'mysql:mysql-connector-java:5.1.39'
}

//afterReleaseBuild.dependsOn uploadArchives

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

configurations { deployerJars }

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Fuzzy CSV'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A groovy/java tabular Data (from CSV,SQL,JSON) processing library ' +
                        'that supports fuzzy column matching,tranformations/merging/querying etc'
                url 'https://github.com/kayr/fuzzy-csv/'

                scm {
                    connection 'scm:git:https://github.com/kayr/fuzzy-csv.git'
                    developerConnection 'scm:git:https://github.com/kayr/fuzzy-csv.git'
                    url 'https://github.com/kayr/fuzzy-csv.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'kayr'
                        name 'Ronald Kayondo'
                        email 'noryak1234@gmail.com'
                    }
                }
            }
        }
    }
}
